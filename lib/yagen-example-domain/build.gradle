/*
 Copyright 2014 Georg Kohlweiss

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

 http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
*/

dependencies {
    compile project(':lib:yagen-generator-lib')

    compile 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'

    runtime 'org.slf4j:slf4j-log4j12:1.6.1'
    runtime 'joda-time:joda-time-hibernate:1.3'
}

sourceSets {
    generated {
        java { srcDirs = [
            "$buildDir/generated-src/main/java"
          ] }
    compileClasspath += sourceSets.main.output
    }
}

compileJava << {
    javaexec {
        main = 'com.github.gekoh.yagen.hst.CreateEntities'
        classpath = sourceSets.main.runtimeClasspath
        args = [
            sourceSets.generated.java.srcDirs.iterator().next().toString(),
            "$projectDir/src/main/resources/META-INF/example-domain.orm.xml",
            "$buildDir/resources/main/META-INF/example-domain-hst.orm.xml"
        ]
    }
}

task compileGenerated(dependsOn: processResources, type: JavaCompile) {
    source = sourceSets.generated.java.srcDirs
    classpath = sourceSets.main.runtimeClasspath
    destinationDir = sourceSets.main.output.classesDir
}

task generateDDL(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.runtimeClasspath
    options.docletpath = sourceSets.main.runtimeClasspath.asType(List)
    options.doclet = "com.github.gekoh.yagen.ddl.comment.CommentsDDLGenerator"
    options.addStringOption("-profile-name", "anyName")
    options.addStringOption("-persistence-unit-name", "generation-unit")
    options.addStringOption("-output-file", "$buildDir/resources/main/META-INF/schema-objects-generated.ddl.sql")
}

jar.dependsOn compileGenerated, generateDDL
